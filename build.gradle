import static org.gradle.api.JavaVersion.VERSION_1_7

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.+'
    }
}

targetCompatibility = VERSION_1_7

group = 'com.github.zhurlik'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // swagger plugin
    compile group: 'io.swagger', name: 'swagger-core', version: '1.5.10'
    compile group: 'com.github.kongchen', name: 'swagger-maven-plugin', version: '3.1.4'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: $descriptor")
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: $descriptor produced standard out/err: ${event.message}")
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'https://github.com/zhurlik/gradle-swagger-plugin'
    vcsUrl = 'https://github.com/zhurlik/gradle-swagger-plugin'
    description = 'A gradle plugin for using swagger-maven-plugin'
    tags = ['java', 'swagger', 'maven', 'gradle', 'plugin', 'groovy']

    plugins {
        jbossModulesPlugin {
            id = 'com.github.zhurlik.swagger'
            displayName = 'Gradle plugin for using swagger-maven-plugin'
        }
    }
}
